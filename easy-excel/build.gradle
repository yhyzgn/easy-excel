import java.text.SimpleDateFormat

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    google()
}

apply from: "../ext.gradle"

group rootProject.ext.groupId
version rootProject.ext.version

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'

    api 'org.apache.poi:poi:4.1.2'
    api 'org.apache.poi:poi-ooxml:4.1.2'
    api 'org.apache.poi:poi-ooxml-schemas:4.1.2'
    api 'com.hankcs:hanlp:portable-1.7.8'

    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'org.jetbrains:annotations:20.1.0'
    compileOnly 'org.slf4j:slf4j-api:2.0.0-alpha1'
    compileOnly 'org.projectlombok:lombok:1.18.16'
    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc
}

compileJava.inputs.files(processResources)

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.warnings = false
    options.deprecation = true
    options.compilerArgs += ["-parameters"]
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

jar {
    enabled true
    afterEvaluate {
        manifest {
            attributes 'Implementation-Version': archiveVersion
            attributes 'Built-Gradle': gradle.gradleVersion
            attributes 'Bundle-DocURL': "https://${rootProject.ext.url}"
            attributes 'Build-OS': System.getProperty("os.name")
            attributes 'Built-By': System.getProperty("user.name")
            attributes 'Build-Jdk': System.getProperty("java.version")
            attributes 'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())
        }
    }
}

javadoc {
    description = "生成jar格式的javadoc。"
    // 只显示 protected 和 public 的类和成员
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.header = project.name
    // 静默javadoc检查（比如不支持@date会报错等），jdk 8+
    options.addStringOption('Xdoclint:none', '-quiet')
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
    // suppress warnings due to cross-module @see and @link references;
    // note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    // suppress "## warnings" message
    logging.captureStandardOutput LogLevel.INFO
    // 编码一定要配置否则直接出错
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    // java9
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootProject.ext.groupId
            artifactId = rootProject.ext.artifactId
            version = rootProject.ext.version
            from components.java

            pom {
                name = rootProject.ext.artifactId
                description = rootProject.ext.description
                url = "https://${rootProject.ext.url}"
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }
                developers {
                    developer {
                        name = 'yhyzgn'
                        email = 'yhyzgn@gmail.com'
                        url = "https://github.com/yhyzgn/"
                    }
                }
                scm {
                    url = "https://${rootProject.ext.url}.git"
                    connection = "scm:git:https://${rootProject.ext.url}.git"
                    developerConnection = "scm:git:https://${rootProject.ext.url}.git"
                }
            }
        }
    }

    repositories {
        maven {
            name "oss"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
//                username = ossrhUsername
//                password = ossrhPassword
            }
        }
    }
}

//signing {
//    sign publishing.publications.mavenJava
//}